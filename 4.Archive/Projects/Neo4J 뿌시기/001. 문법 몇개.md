#토이프로젝트 #graphfs
### 구조
```
root
├── child1
│   ├── child1-1
│   └── child1-2
├── child2
│   ├── child2-1
│   ├── child2-2
│   │   ├── child2-2-1
│   │   └── child2-2-2
│   └── child2-3
└── child3
    └── child3-1
        └── child3-1-1
            └── child3-1-1-1
```
### 조회
#### 모든 폴더 노드 조회
```cypher
MATCH (f:Folder) RETURN f
```
#### 가장 빠른 경로
```cypher
MATCH
	(anc:Folder),
	(des:Folder),
	path = shortestPath((anc)-[:DIRECT_CHILD*]->(des))
WHERE id(anc) = 0 AND id(des) = 6
RETURN path
```
또는
```cypher
MATCH
	(anc:Folder { id: 'root-id' }),
	(des:Folder { id: 'child-3-1-1-1' }),
	path = shortestPath((anc)-[:DIRECT_CHILD*]->(des))
RETURN path
```
#### 경로 상에 있는 모든 자식 노드들
```cypher
MATCH p=(anc:Folder)-[:DIRECT_CHILD*]->(des:Folder)
WHERE anc.id = 'root-id' AND des.id = 'child-2-2-2'

WITH nodes(p) as nodesByPath
MATCH (parent:Folder)-[:DIRECT_CHILD]->(children:Folder)
WHERE parent IN nodesByPath
RETURN children.name
```
### 생성
#### 신규 노드 생성
```cypher
CREATE (f:Folder{name: 'root',isRoot:true, projectId: 'test-v1'}) RETURN f
```
#### 노드 생성과 동시에 관계 형성
```cypher
MATCH (f:Folder {id:'root-id'}) CREATE (f) -[:DIRECT_CHILD]-> (child:Folder {id:'child-1',isRoot:false,name:'child1',projectId:'test-v1'}) return child
```
### 수정
#### 프로퍼티 추가
```cypher
MATCH (f) WHERE f.name = 'root' SET f:Folder, f.id = 'root-id' RETURN f
```
### 삭제
#### 임의의 노드 및 모든 관계 제거
```cypher
MATCH (f:Folder {id: 'child-3'})
DETACH DELETE f
```
### 기타
#### 결과 존재 여부 반환
- exists 함수 사용
```cypher
MATCH (drive: Drive) WHERE drive.id = '95d99609-2281-4b70-83c3-82f7d168d68a' RETURN exists((drive)-[:DIRECT_CHILD]->())
```
#### 임의의 노드로부터 가장 멀리 떨어진 노드
```cypher
MATCH (:Folder { name: '3' })-[:CHILD_OF*0..]->(n: Folder) WHERE NOT exists((n)-[:CHILD_OF]->()) return n
```
---
### 참고
#### depth 조회
- `-[r:RELATION_NAME*]->` 관계를 끝까지 타고 나감
- `-[r:RELATION_NAME]->` 직접 관계까지만 조회
- `-[r:RELATION_NAME*0..]->` 관계를 끝까지 타고 나가되, 자기 자신을 포함
```cypher
MATCH p = (f: Folder { name: 'root' })-[:DIRECT_CHILD*]->(n:Folder)
RETURN n, LENGTH(p) as myDepth
```
#### 노드 이동(child-2를 child-1-1의 자식으로)
> [!warning] 궁금증
> 쿼리를 너무 막 만드는 느낌이 드는데... 실행 계획은 어떻게 체크해야할까?
- child-2 폴더를 f1으로, f1의 직계 부모 노드와의 관계를 r로 설정
- child-1-1을 f2로 설정하되, f1이 f2의 자식이 아닌 노드만 조회(자신의 자손 폴더로는 이동 불가)
- f2와 f1의 관게를 새로 맺고, f1이 기존에 갖고 있던 관계 r은 제거
- f1을 포함한 모든 자손의 depth를 옮겨진 곳 기준으로 수정
```cypher
MATCH ()-[r:DIRECT_CHILD]->(f1: Folder { name: 'child-2' }) 

WITH f1, r 
MATCH (f2: Folder { name: 'child-1-1' }) 
WHERE NOT (f1)-[:DIRECT_CHILD*0..]->(f2) 
CREATE (f2)-[:DIRECT_CHILD]->(f1) 
DELETE r 
				 
WITH f1, f2.depth - f1.depth + 1 as increment 
MATCH (f1)-[:DIRECT_CHILD*0..]->(folders: Folder) 
SET folders.depth = folders.depth + increment;
```